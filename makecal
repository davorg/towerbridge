#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use Data::Dumper;
use Time::Piece;
use Web::Query;
use Date::ICal;
use Data::ICal;
use Data::ICal::Entry::Event;

my $i = 0;

my $now = localtime;
my ($curr_mon, $curr_year) = ($now->mon, $now->year);

my $ical = Data::ICal->new();
my $fmt = '%H:%M %d %b %Y';

my @lifts;

wq('http://www.towerbridge.org.uk/lift-times/')
  ->find('table.lined tbody tr')
  ->each(sub { push @lifts, [ map { $_->text } $_[1]->contents ] });

foreach (@lifts) {
  my $date = Time::Piece->strptime("$_->[2] $_->[1] $curr_year", $fmt);
  # If the month number of this event is less than the current month
  # number then we've gone to the next year. Increment the year number
  # and re-calculate.
  if ($date->mon < $curr_mon) {
    ++$curr_year;
    $date = Time::Piece->strptime("$_->[2] $_->[1] $curr_year", $fmt);
  }
  my $event = Data::ICal::Entry::Event->new();
  $event->add_properties(
    summary => 'Tower Bridge Lift',
    description => "$_->[3] ($_->[4])",
    dtstart => Date::ICal->new(epoch => $date->epoch)->ical,
    duration => 0,
  );
  $ical->add_entry($event);
}

open my $ical_fh, '>', 'towerbridge.ics' or die $!;
print $ical_fh $ical->as_string;
