#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use Data::Dumper;
use DateTime;
use DateTime::Format::Strptime;
use Web::Query;
use JSON;
use Data::ICal;
use Data::ICal::Entry::Event;
use DateTime::Format::ICal;

my $i = 0;

my $now = DateTime->now(time_zone => 'Europe/London');
my ($curr_mon, $curr_year) = ($now->month, $now->year);

my $ical = Data::ICal->new();
my $dt_parser = DateTime::Format::Strptime->new(
  pattern => '%H:%M %d %b %Y',
);

my $now_ical = dt2ical($now);

my @lifts;

wq('http://www.towerbridge.org.uk/lift-times/')
  ->find('table.lined tbody tr')
  ->each(sub { push @lifts, [ map { $_->text } $_[1]->contents ] });

my %seen;
my @json;

foreach (@lifts) {
  my $date = $dt_parser->parse_datetime("$_->[2] $_->[1] $curr_year");
  # If the month number of this event is less than the current month
  # number then we've gone to the next year. Increment the year number
  # and re-calculate.
  if ($date->mon < $curr_mon) {
    ++$curr_year;
    $date = $dt_parser->parse_datetime("$_->[2] $_->[1] $curr_year");
  }

  # Tower Bridge web site occasionally has duplicates
  next if $seen{$date->epoch}++;

  my $event = Data::ICal::Entry::Event->new();
  $event->add_properties(
    summary => 'Tower Bridge Lift',
    description => "$_->[3] ($_->[4])",
    dtstart => dt2ical($date),
    duration => 'PT30M',
    dtstamp => $now_ical,
    uid => $date->epoch . '@towerbridge.dave.org.uk',
  );
  $ical->add_entry($event);

  my $lift;
  $lift->{datetime}  = $date->iso8601 . $date->strftime('%z');
  $lift->{vessel}    = $_->[3];
  $lift->{direction} = $_->[4];

  push @json, $lift;
}

open my $ical_fh, '>', 'towerbridge.ics' or die $!;
my $ical_str = $ical->as_string;
my $timezone = do { local $/; <DATA> };
chomp($timezone);
$timezone =~ s/\n/\015\012/g;
$ical_str =~ s/(BEGIN:VCALENDAR)/$1\015\012$timezone/;
print $ical_fh $ical_str;

open my $json_fh, '>', 'towerbridge.json' or die $!;
print $json_fh JSON->new->pretty->encode({ lifts => \@json });

sub dt2ical {
  my ($dt) = @_;

  return [ $dt->strftime('%Y%m%dT%H%M%S'), { TZID => 'Europe/London' } ];
}

__END__
BEGIN:VTIMEZONE
TZID:Europe/London
BEGIN:STANDARD
DTSTART:19710101T020000
TZOFFSETTO:+0000
TZOFFSETFROM:+0100
RRULE:FREQ=YEARLY;WKST=MO;INTERVAL=1;BYMONTH=10;BYDAY=-1SU
END:STANDARD
BEGIN:DAYLIGHT
DTSTART:19710101T010000
TZOFFSETTO:+0100
TZOFFSETFROM:+0000
RRULE:FREQ=YEARLY;WKST=MO;INTERVAL=1;BYMONTH=3;BYDAY=-1SU
END:DAYLIGHT
END:VTIMEZONE
